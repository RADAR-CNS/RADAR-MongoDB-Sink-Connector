plugins {
    // Apply the java plugin to add support for Java
    id 'java'
    id 'idea'
    id 'jacoco'
    id 'checkstyle'
    id 'pmd'
    id "com.jfrog.bintray" version "1.7.3"
    id 'maven-publish'
}

version '0.1-alpha.1-SNAPSHOT'
group = 'org.radarcns'
ext.vcsUrl = 'https://github.com/RADAR-CNS/RADAR-MongoDB-Sink-Connector.git'

targetCompatibility = '1.8'
sourceCompatibility = '1.8'

ext.radarCommonsVersion = '0.3-SNAPSHOT'
ext.radarSchemaSdkVersion = '0.1'
ext.mongoDbSinkVersion = 'v0.1-alpha.1'
ext.junitVersion = '4.12'
ext.codacyVersion = '1.0.10'
ext.openCsvVersion = '3.9'
ext.mathVersion = '3.0'



//---------------------------------------------------------------------------//
// Sources and classpath configurations                                      //
//---------------------------------------------------------------------------//

sourceSets {
    // Note that just declaring this sourceset creates two configurations.
    endToEndTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
        }
        resources {
            srcDir 'src/endToEndTest/resources'
        }
    }
}

configurations {
    codacy
    endToEndTestCompile.extendsFrom testCompile
    endToEndTestRuntime.extendsFrom testRuntime
}

repositories {

    // For working with dev-branch commons
    maven { url "${rootProject.projectDir}/libs" }

    jcenter()
    maven { url 'http://packages.confluent.io/maven/' }
    maven { url 'https://jitpack.io' }
    maven { url 'http://dl.bintray.com/typesafe/maven-releases' }
}

dependencies {
    compile group: 'org.radarcns', name: 'radar-commons', version: radarCommonsVersion
    compile group: 'org.radarcns', name: 'radar-schemas-commons', version: radarSchemaSdkVersion
    compile group: "org.radarcns", name: 'kafka-connect-mongodb-sink', version: mongoDbSinkVersion

    endToEndTestCompile  group: 'com.opencsv', name: 'opencsv', version: openCsvVersion
    endToEndTestCompile group: 'org.apache.commons', name: 'commons-math3', version: mathVersion
    endToEndTestCompile group: 'org.radarcns', name: 'radar-commons-testing', version: radarCommonsVersion
    testCompile group: 'junit', name: 'junit', version: junitVersion

    codacy group: 'com.github.codacy', name: 'codacy-coverage-reporter', version: codacyVersion
}

jar {
    manifest {
        attributes 'Implementation-Title': 'RADAR-CNS MongoDB Connector',
                'Implementation-Version': version
    }
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

//---------------------------------------------------------------------------//
// Style checking                                                            //
//---------------------------------------------------------------------------//

checkstyle {
    // codacy version
    toolVersion '6.16'
    ignoreFailures false

    // ignore tests
    sourceSets = [sourceSets.main]
}

pmd {
    // codacy version
    toolVersion = '5.5.2'
    ignoreFailures = false

    // ignore tests
    sourceSets = [sourceSets.main]

    consoleOutput = true

    ruleSets = []
    ruleSetFiles = files("config/pmd/ruleset.xml")
}

//---------------------------------------------------------------------------//
// Testing                                                                   //
//---------------------------------------------------------------------------//

tasks.matching {it instanceof Test}.all {
    def stdout = new LinkedList<String>()
    beforeTest { TestDescriptor td ->
        stdout.clear()
    }

    onOutput { TestDescriptor td, TestOutputEvent toe ->
        stdout.addAll(toe.getMessage().split('(?m)$'))
        while (stdout.size() > 100) {
            stdout.remove()
        }
    }

    afterTest { TestDescriptor td, TestResult tr ->
        if (tr.resultType == TestResult.ResultType.FAILURE) {
            println()
            print("${td.className}.${td.name} FAILED")
            if (stdout.empty) {
                println(" without any output")
            } else {
                println(" with last 100 lines of output:")
                println('=' * 100)
                stdout.each { print(it) }
                println('=' * 100)
            }
        }
    }

    testLogging {
        showExceptions = true
        showCauses = true
        showStackTraces = true
        exceptionFormat "full"
    }
}

//---------------------------------------------------------------------------//
// End to End Test                                                           //
//---------------------------------------------------------------------------//
task endToEndTest(type: Test){
    description = "Run integration tests (located in src/endToEndTest/...)."
    testClassesDir project.sourceSets.endToEndTest.output.classesDir
    classpath = project.sourceSets.endToEndTest.runtimeClasspath
    // This is not needed, but I like to see which tests have run
    testLogging {
        events "skipped", "failed"
    }
}

task setRadarEnvironmentForDocker {
    doLast {
        File volumes = new File("src/endToEndTest/radarDocker/volumes")
        volumes.mkdir()
        String volumePath = volumes.getAbsolutePath()

        def fileEnv = new File("src/endToEndTest/radarDocker/dcompose-stack/radar-cp-hadoop-stack/.env")
        fileEnv.text = fileEnv.text.replace('/usr/local/var/lib/docker', volumePath)
        fileEnv.text = fileEnv.text.replace('HOTSTORAGE_USERNAME=<mongodb-user>', 'HOTSTORAGE_USERNAME=restapi')
        fileEnv.text = fileEnv.text.replace('HOTSTORAGE_PASSWORD=XXXXXXXX', 'HOTSTORAGE_PASSWORD=radarcns')
        fileEnv.text = fileEnv.text.replace('HOTSTORAGE_NAME=<mongodb-database>', 'HOTSTORAGE_NAME=hotstorage')

        def fileInstall = new File("src/endToEndTest/radarDocker/dcompose-stack/radar-cp-hadoop-stack/install-radar-stack.sh")
        fileInstall.text = fileInstall.text.replace('/usr/local/var/lib/docker', volumePath)

        new File("src/endToEndTest/radarDocker/dcompose-stack/radar-cp-hadoop-stack/smtp.env.template").renameTo("src/endToEndTest/radarDocker/dcompose-stack/radar-cp-hadoop-stack/smtp.env")
    }
}



//---------------------------------------------------------------------------//
// Build system metadata                                                     //
//---------------------------------------------------------------------------//
idea {
    module {
        downloadSources = true
    }
}

jacocoTestReport {
    executionData test, endToEndTest
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled false
    }
}

task downloadDependencies(type: Exec) {
    configurations.testRuntime.files
    configurations.jacocoAnt.files
    configurations.codacy.files
    commandLine 'echo', 'Downloaded all dependencies'
}

task sendCoverageToCodacy(type: JavaExec, dependsOn: jacocoTestReport) {
    main = 'com.codacy.CodacyCoverageReporter'
    classpath = configurations.codacy
    args = ['-l', 'Java', '-r', "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"]
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution rootProject.name
        }
    }
    developers {
        developer {
            id "radar-cns"
            name "Nivethika"
            email "nivethika@thehyve.nl"
        }
    }
}
// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.output
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar, javadocJar
}


publishing {
    publications {
        RadarPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'org.radarcns'
            artifactId 'radar-mongodb-sink-connector'
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'A MongoDB Sink connector for RADAR-CNS platform.')
                root.appendNode('name', rootProject.name)
                root.appendNode('url', vcsUrl)
                root.children().last() + pomConfig
            }
        }
    }
}



bintray {
    user = System.getenv('BINTRAY_USER')? System.getenv('BINTRAY_USER') : project.property('bintrayUser')
    key = System.getenv('BINTRAY_API_KEY') ? System.getenv('BINTRAY_API_KEY') : project.property('bintrayApiKey')
    override = false
    publications = ['RadarPublication']
    pkg {
        repo = project.group
        name = rootProject.name
        userOrg = 'radar-cns'
        licenses = ['Apache-2.0']
        vcsUrl = vcsUrl
        version {
            name = project.version
            desc = 'RADAR-MongoDb-Sink-Connector: Initial release'
            vcsTag = System.getenv('TRAVIS_TAG')
            released  = new Date()
        }
    }
}


task wrapper(type: Wrapper) {
    gradleVersion = '3.4.1'
    distributionUrl distributionUrl.replace("bin", "all")
}